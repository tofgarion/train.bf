import Bf.Init



/-! # Monads and `do` notation -/
namespace Zen.Train.Trash.MonadsAndDo



/-! A monad wraps values of any type `Œ±` and is equipped with two operations. -/

/-- For instance: -/
abbrev Just : Type ‚Üí Type := id

#check Just

namespace Just
def get : Just Œ± ‚Üí Œ± := id

/-- Injects a value into the monad. -/
protected def pure (val : Œ±) : Just Œ± :=
  val

/-- Given a `Just Œ±`-value, apply a function to the inner `Œ±` that returns a `Just Œ≤`-value. -/
protected def bind (justVal : Just Œ±) (f : Œ± ‚Üí Just Œ≤) : Just Œ≤ :=
  f justVal


/-! Now check this out.

```
let mut val := 7
if 0 < val then
  val := val - 1
val := val % 2
```

This is obviously not compatible with a pure language. Or is it?
-/

def encodeLetMut (val : Nat) : Just Nat :=
  Just.pure val
  |>.bind (fun val =>
    let val := if 0 < val then val - 1 else val
    Just.pure val
  )
  |>.bind (fun val =>
    Just.pure (val % 2)
  )

example : encodeLetMut 7 = Just.pure 0 := rfl
example : encodeLetMut 0 = Just.pure 0 := rfl
example : encodeLetMut 8 = Just.pure 1 := rfl



/-! This is basically how the `do` notation works for `Monad`s. -/

#checkout Monad

instance instMonad : Monad Just where
  pure := Just.pure
  bind := Just.bind

def doLetMut (val : Nat) : Just Nat := do
  let mut val := val
  if 0 < val then
    val := val - 1
  return val % 2 -- same as `pure (val % 2)`

end Just



/-! ## `do` notation: `‚Üê`

Define an option type `Opt : Type ‚Üí Type` with variants `non`e and `som`e.

(To avoid clashing with Lean's `Option` type).
-/

inductive Opt : Type ‚Üí Type
| non : Opt Œ±
| som : Œ± ‚Üí Opt Œ±

#check Opt.non
#check Opt.som

namespace Opt

/-! Write a `ToString` instance for `Opt`. -/
instance instToString [ToString Œ±] : ToString (Opt Œ±) where
  toString
    | non   => "none"
    | som a => toString a

/-! Write a `Monad` instance for `Opt`. -/
instance instToMonad : Monad Opt where
  pure a :=
    som a
  bind a? f? :=
    match a? with
      | non   => non
      | som a => f? a

/-! We can use `‚Üê` to "access the value", *i.e.* trigger a bind. -/

def map (a? : Opt Œ±) (f : Œ± ‚Üí Œ≤) : Opt Œ≤ := do
  let a ‚Üê a? -- using `‚Üê` instead of `:=`
  return f a -- again, `return <term>` is just `pure (<term>)`

def map' (a? : Opt Œ±) (f : Œ± ‚Üí Œ≤) : Opt Œ≤ := do
  -- you can also do this
  return f (‚Üê a?)

def mapWithBind (a? : Opt Œ±) (f : Œ± ‚Üí Œ≤) : Opt Œ≤ :=
  -- `bind a? (pure ‚àò f)`, but let's write it with the infix `>>=` bind operator to look cool
  a? >>= pure ‚àò f


def demo (a? : Opt Nat) : Opt Nat := do
  if let 0 ‚Üê a?
  then non else
    match ‚Üê a? with
    | 0 => panic! "unreachable!"
    | 7 => return 7
    | _ => non

/-! `#eval` actually takes a term of type `IO Œ±`, and `IO` is a monad. -/
#checkout IO
#checkout IO.println

#eval do
  let mut val := demo (som 0)
  -- same as `IO.println` but takes an interpolated string
  println! "{val}"
  -- has type `IO Unit`, `do` notation lets us chain `M Unit` expressions when `M` is a monad

  val := demo (som 1) -- also has type `M Unit` (`IO Unit`, here)
  println! "{val}"
  val := demo (som 7)
  println! "{val}"
  return ()

end Opt



/-! ## State monads

Exercise / discussion

In a purely functional language, how would you write a function that produces some result `Œ±` but
also

- just reads some state?
- reads and write some state?
-/

structure State where
  counter : Nat

def justReads (params : Nat) (state : State) : Nat :=
  params + state.counter

def readsWrites (params : Nat) (state : State) : Nat √ó State :=
  let out := justReads params state
  let state := { state with counter := state.counter + 1 }
  (out, state)

def justReads' (params : Nat) : State ‚Üí Nat
| state => params + state.counter

def readsWrites' (params : Nat) : (state : State) ‚Üí Nat √ó State
| state =>
  let out := justReads params state
  let state := { state with counter := state.counter + 1 }
  (out, state)


/-! What would be the corresponding monads `ReadM` and `WriteM` for some generic state type `œÉ`? -/

abbrev ReadM (œÉ : Type) (Œ± : Type) : Type :=
  œÉ ‚Üí Œ±

abbrev WriteM (œÉ : Type) (Œ± : Type) : Type :=
  œÉ ‚Üí Œ± √ó œÉ

/-! Now just write `Monad` instances for them. -/

instance instReadM : Monad (ReadM œÉ) where
  pure a _ := a
  -- ou ùïÇ a
  bind a? f? :=
    fun s =>
      let a := a? s
      f? a s

instance instWriteM : Monad (WriteM œÉ) where
  pure a :=
    fun s => ‚ü® a, s ‚ü©
  bind a? f? :=
    fun s =>
      let ‚ü® a, s' ‚ü© := a? s
      f? a s'



/-! Awesome, now we can define a few helpers. -/

def ReadM.get : ReadM œÉ œÉ
| state => state

def WriteM.get : WriteM œÉ œÉ
| state => (state, state)

def WriteM.set : œÉ ‚Üí WriteM œÉ Unit
| newState, _oldState => ((), newState)



abbrev Log := Array String

def WriteM.demo (n : Nat) : WriteM Log Nat := do
  log "checking if `n` is zero"
  if n = 0 then
    log' "`n` is zero!"
    return n
  else
    log "`n` is not zero, modulo-2-ing it"
    return n % 2
where
  log (msg : String) : WriteM Log Unit := do
    let log ‚Üê get
    set <| log.push msg
  /-- Arguably simpler, arguably not as fancy. -/
  log' (msg : String) : WriteM Log Unit
    | log => ((), log.push msg)


/-! Write `WriteM.runDemo : IO Unit` that runs `WriteM.demo` on some inputs, shows the results and
log for each call.
-/

def WriteM.runDemo : IO Unit := do
  for input in [0, 1, 2, 3, 4, 15, 20] do
    let state := #[]
    let (res, state) := demo input state
    println! "demo {input}"
    for line in state do
      println! "- {line}"
    println! "‚Ü¶ {res}"
    println! ""

#eval WriteM.runDemo

end MonadsAndDo



/-! The `Just` monad is called `Id` in Lean. -/
#checkout Id
#checkout (inferInstance : Monad Id) -- would fail if no `Monad Id` instances existed



/-! The cool thing is that we can run imperative-looking code whenever we want with `Id`. -/
#checkout Id.run

/-- Array pretty-printer (`pp`). -/
private
def ppArray [ToString Œ±]
  (array : Array Œ±)
  (sep : String := ", ")
  (delim : String √ó String := ("#[", "]"))
: String := Id.run do
  let mut s := ""
  for elm in array do
    if ¬¨ s.isEmpty then
      s := s ++ sep
    s := s ++ toString elm
  delim.1 ++ s ++ delim.2

/-- info:
#[1, 2, 3]
#[1 - 2 - 3]
<| 1 | 2 | 3 |>
<| 1 | 2 | 3 |>
‚ü¶1 ¬∑ 2 ¬∑ 3‚üß
-/
#guard_msgs in #eval do
  let array := #[1, 2, 3]
  println! "{ppArray array}"
  let s := ppArray array " - "
  println! "{s}"
  let s := ppArray array " | " ("<| ", " |>")
  println! "{s}"
  let s := ppArray array " | " ("<| ", " |>")
  println! "{s}"
  let s := ppArray (delim := ("‚ü¶", "‚üß")) (sep := " ¬∑ ") array
  println! "{s}"



/-! ## Monad transformers

Now, what if we want a state monad that allows operations from other monads such as `IO`, or the
*result* monad `Except Œµ`?
-/

abbrev BadSMonT (œÉ : Type) (M : Type ‚Üí Type) (Œ± : Type) : Type :=
  M (œÉ ‚Üí Œ± √ó œÉ)

abbrev SMonT (œÉ : Type) (M : Type ‚Üí Type) (Œ± : Type) : Type :=
  œÉ ‚Üí M (Œ± √ó œÉ)

namespace SMonT
instance instMonad [inst : Monad M] : Monad (SMonT œÉ M) where
  pure a := fun state => inst.pure (a, state)
  bind a? f? state := do
    let (a, state) ‚Üê a? state
    f? a state

variable [Monad M]

/-! Write the following functions. -/

def get : SMonT œÉ M œÉ :=
    fun state => pure (state, state)

/-- info: Zen.Train.Trash.SMonT.get {M : Type ‚Üí Type} [Monad M] {œÉ : Type} : SMonT œÉ M œÉ -/
#guard_msgs in #check get

def set (state : œÉ) : SMonT œÉ M Unit :=
  fun _ => pure ((), state)

/-- info:
Zen.Train.Trash.SMonT.set {M : Type ‚Üí Type} [Monad M] {œÉ : Type} (state : œÉ) : SMonT œÉ M Unit
-/
#guard_msgs in #check set

def printState [ToString œÉ] : SMonT œÉ IO Unit :=
  fun s => do
    println! "state: {s}"
    return ((), s)

/-- info: Zen.Train.Trash.SMonT.printState {œÉ : Type} [ToString œÉ] : SMonT œÉ IO Unit -/
#guard_msgs in #check printState

/-- info:
state: 7
final state is `7`
-/
#guard_msgs in #eval do
  let state := 7
  let (_, state) ‚Üê printState state
  println! "final state is `{state}`"



/-! It's tedious to have to "deconstruct" our monad transformer to run `M`-code.

Check out this class, and write the appropriate instance.
-/
#checkout MonadLift

instance [Monad M] : MonadLift M (SMonT œÉ M) where
  monadLift a? :=
    fun s => do
      let a ‚Üê a?
      return (a, s)

/-! This gives access to `liftM` which (here) can lift `IO`-code to `SMonT œÉ IO`-code. -/

def printState' [ToString œÉ] : SMonT œÉ IO Unit := do
  let state ‚Üê get
  liftM $ println! "state: {state}"

/-- info:
state: 7
final state is `7`
-/
#guard_msgs in #eval do
  let state := 7
  let (_, state) ‚Üê printState' state
  println! "final state is `{state}`"



/-! What if `M` is not really `IO`, but just something that `MonadLift`-s from `IO`? -/
def printState'' [MonadLift IO M] [ToString œÉ] : SMonT œÉ M Unit := do
  let state ‚Üê get
  liftM $ println! "state: {state}"

/-! This is too easy, now we can write functions like these. Right? -/

/-- error:
failed to synthesize instance
  MonadLift IO IO
-/
#guard_msgs in
def printState''IO [ToString œÉ] : SMonT œÉ IO Unit :=
  printState''

/-! We can't üôÄ

Let's actually check `liftM`'s type:
-/
#checkout liftM
/-! which leads us to: -/
#checkout MonadLiftT

/-! Let's try this again. -/
def printState''' [MonadLiftT IO M] [ToString œÉ] : SMonT œÉ M Unit := do
  let state ‚Üê get
  liftM $ println! "state: {state}"

def printState'''IO [ToString œÉ] : SMonT œÉ IO Unit :=
  printState'''

/-! Nice. -/

end SMonT
