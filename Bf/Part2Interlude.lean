import Bf.Init



/-! # Monads and `do` notation -/
namespace Zen.Train.Trash.MonadsAndDo



/-! A monad wraps values of any type `Î±` and is equipped with two operations. -/

/-- For instance: -/
abbrev Just : Type â†’ Type := id

#check Just

namespace Just
def get : Just Î± â†’ Î± := id

/-- Injects a value into the monad. -/
protected def pure (val : Î±) : Just Î± :=
  val

/-- Given a `Just Î±`-value, apply a function to the inner `Î±` that returns a `Just Î²`-value. -/
protected def bind (justVal : Just Î±) (f : Î± â†’ Just Î²) : Just Î² :=
  f justVal


/-! Now check this out.

```
let mut val := 7
if 0 < val then
  val := val - 1
val := val % 2
```

This is obviously not compatible with a pure language. Or is it?
-/

def encodeLetMut (val : Nat) : Just Nat :=
  Just.pure val
  |>.bind (fun val =>
    let val := if 0 < val then val - 1 else val
    Just.pure val
  )
  |>.bind (fun val =>
    Just.pure (val % 2)
  )

example : encodeLetMut 7 = Just.pure 0 := rfl
example : encodeLetMut 0 = Just.pure 0 := rfl
example : encodeLetMut 8 = Just.pure 1 := rfl



/-! This is basically how the `do` notation works for `Monad`s. -/

#checkout Monad

instance instMonad : Monad Just where
  pure := Just.pure
  bind := Just.bind

def doLetMut (val : Nat) : Just Nat := do
  let mut val := val
  if 0 < val then val := val - 1
  return val % 2 -- same as `pure (val % 2)`

end Just



/-! ## `do` notation: `â†`

Define an option type `Opt : Type â†’ Type` with variants `non`e and `som`e.

(To avoid clashing with Lean's `Option` type).
-/

section sol!
inductive Opt (Î± : Type) : Type
| non
| som : Î± â†’ Opt Î±
end sol!

#check Opt.non
#check Opt.som

namespace Opt

/-! Write a `ToString` instance for `Opt`. -/
section sol!
instance instToString [ToString Î±] : ToString (Opt Î±) where
  toString
    | non => "none"
    | som a => toString a
end sol!

/-! Write a `Monad` instance for `Opt`. -/
section sol!
instance instMonad : Monad Opt where
  pure := som
  bind
  | non, _ => non
  | som a, f => f a
end sol!



/-! We can use `â†` to "access the value", *i.e.* trigger a bind. -/

def map (a? : Opt Î±) (f : Î± â†’ Î²) : Opt Î² := do
  let a â† a? -- using `â†` instead of `:=`
  return f a -- again, `return <term>` is just `pure (<term>)`

def map' (a? : Opt Î±) (f : Î± â†’ Î²) : Opt Î² := do
  -- you can also do this
  return f (â† a?)

def mapWithBind (a? : Opt Î±) (f : Î± â†’ Î²) : Opt Î² :=
  -- `bind a? (pure âˆ˜ f)`, but let's write it with the infix `>>=` bind operator to look cool
  a? >>= pure âˆ˜ f


def demo (a? : Opt Nat) : Opt Nat := do
  if let 0 â† a?
  then non else
    match â† a? with
    | 0 => panic! "unreachable!"
    | 7 => return 7
    | _ => non

/-! `#eval` actually takes a term of type `IO Î±`, and `IO` is a monad. -/
#checkout IO
#checkout IO.println

#eval do
  let mut val := demo (som 0)
  -- same as `IO.println` but takes an interpolated string
  println! "{val}"
  -- has type `IO Unit`, `do` notation lets us chain `M Unit` expressions when `M` is a monad

  val := demo (som 1) -- also has type `M Unit` (`IO Unit`, here)
  println! "{val}"
  val := demo (som 7)
  println! "{val}"
  return ()

end Opt



/-! ## State monads

Exercise / discussion

In a purely functional language, how would you write a function that produces some result `Î±` but
also

- just reads some state?
- reads and write some state?
-/

structure State where
  counter : Nat

section sol!
def justReads (params : Nat) (state : State) : Nat :=
  params + state.counter

-- alternatively
def justReads' (params : Nat) : State â†’ Nat :=
  fun state => params + state.counter

def readsWrites (params : Nat) (state : State) : Nat Ã— State :=
  let out := justReads params state
  let state := { state with counter := state.counter + 1 }
  (out, state)

-- alternatively
def readsWrites' (params : Nat) : State â†’ Nat Ã— State :=
  fun state =>
    let out := justReads params state
    let state := { state with counter := state.counter + 1 }
    (out, state)
end sol!



/-! What would be the corresponding monads `ReadM` and `WriteM` for some generic state type `Ïƒ`? -/

section sol!
abbrev ReadM (Ïƒ : Type) (Î± : Type) : Type :=
  Ïƒ â†’ Î±

abbrev WriteM (Ïƒ : Type) (Î± : Type) : Type :=
  Ïƒ â†’ Î± Ã— Ïƒ
  -- same as `ReadM Ïƒ (Î± Ã— Ïƒ)`
end sol!



/-! Now just write `Monad` instances for them. -/

section sol!
instance ReadM.instMonad : Monad (ReadM Ïƒ) where
  pure a _state := a
  bind a? f? state :=
    let a := a? state
    f? a state

instance WriteM.instMonad : Monad (WriteM Ïƒ) where
  pure := Prod.mk
  bind a? f? state :=
    let (a, state) := a? state
    f? a state
end sol!



/-! Awesome, now we can define a few helpers. -/

def ReadM.get : ReadM Ïƒ Ïƒ
| state => state

def WriteM.get : WriteM Ïƒ Ïƒ
| state => (state, state)

def WriteM.set : Ïƒ â†’ WriteM Ïƒ Unit
| newState, _oldState => ((), newState)



abbrev Log := Array String

def WriteM.demo (n : Nat) : WriteM Log Nat := do
  log "checking if `n` is zero"
  if n = 0 then
    log' "`n` is zero!"
    return n
  else
    log "`n` is not zero, modulo-2-ing it"
    return n % 2
where
  log (msg : String) : WriteM Log Unit := do
    let log â† get
    set <| log.push msg
  /-- Arguably simpler, arguably not as fancy. -/
  log' (msg : String) : WriteM Log Unit
    | log => ((), log.push msg)


/-! Write `WriteM.runDemo : IO Unit` that runs `WriteM.demo` on some inputs, shows the results and
log for each call.
-/

section sol!
def WriteM.runDemo : IO Unit := do
  let state := #[]
  let (res, state) := demo 7 state
  println! "demo 7"
  -- so, turns out we can also `for`-iterate on collections when in a monad ğŸ˜º
  -- see the `ForIn` class for details
  for line in state do
    println! "- {line}"
  println! "â†¦ {res}"
  println! ""

  let state := #[]
  let (res, state) := demo 0 state
  println! "demo 7"
  for line in state do
    println! "- {line}"
  println! "â†¦ {res}"
  println! ""

  let state := #[]
  let (res, state) := demo 10 state
  println! "demo 7"
  for line in state do
    println! "- {line}"
  println! "â†¦ {res}"
end sol!

#eval WriteM.runDemo

end MonadsAndDo



/-! The `Just` monad is called `Id` in Lean. -/
#checkout Id
#checkout (inferInstance : Monad Id) -- would fail if no `Monad Id` instances existed



/-! The cool thing is that we can run imperative-looking code whenever we want with `Id`. -/
#checkout Id.run

/-- Array pretty-printer (`pp`). -/
private
def ppArray [ToString Î±]
  (array : Array Î±)
  (sep : String := ", ")
  (delim : String Ã— String := ("#[", "]"))
: String := Id.run do
  let mut s := ""
  for elm in array do
    if Â¬ s.isEmpty then
      s := s ++ sep
    s := s ++ toString elm
  delim.1 ++ s ++ delim.2

/-- info:
#[1, 2, 3]
#[1 - 2 - 3]
<| 1 | 2 | 3 |>
<| 1 | 2 | 3 |>
âŸ¦1 Â· 2 Â· 3âŸ§
-/
#guard_msgs in #eval do
  let array := #[1, 2, 3]
  println! "{ppArray array}"
  let s := ppArray array " - "
  println! "{s}"
  let s := ppArray array " | " ("<| ", " |>")
  println! "{s}"
  let s := ppArray array " | " ("<| ", " |>")
  println! "{s}"
  let s := ppArray (delim := ("âŸ¦", "âŸ§")) (sep := " Â· ") array
  println! "{s}"



/-! ## Monad transformers

Now, what if we want a state monad that allows operations from other monads such as `IO`, or the
*result* monad `Except Îµ`?
-/

abbrev BadSMonT (Ïƒ : Type) (M : Type â†’ Type) (Î± : Type) : Type :=
  M (Ïƒ â†’ Î± Ã— Ïƒ)

abbrev SMonT (Ïƒ : Type) (M : Type â†’ Type) (Î± : Type) : Type :=
  Ïƒ â†’ M (Î± Ã— Ïƒ)

namespace SMonT
instance instMonad [Monad M] : Monad (SMonT Ïƒ M) where
  pure a state := pure (a, state)
  bind a? f? state := do
    let (a, state) â† a? state
    f? a state

variable [Monad M]

/-! Write the following functions. -/
section sol!
def get : SMonT Ïƒ M Ïƒ
| state => return (state, state)

def set (state : Ïƒ) : SMonT Ïƒ M Unit
| _oldState => return ((), state)

def printState [ToString Ïƒ] : SMonT Ïƒ IO Unit
| state => do
  println! "state: {state}"
  return ((), state)
end sol!

/-- info: Zen.Train.Trash.SMonT.get {M : Type â†’ Type} [Monad M] {Ïƒ : Type} : SMonT Ïƒ M Ïƒ -/
#guard_msgs in #check get
/-- info:
Zen.Train.Trash.SMonT.set {M : Type â†’ Type} [Monad M] {Ïƒ : Type} (state : Ïƒ) : SMonT Ïƒ M Unit
-/
#guard_msgs in #check set
/-- info: Zen.Train.Trash.SMonT.printState {Ïƒ : Type} [ToString Ïƒ] : SMonT Ïƒ IO Unit -/
#guard_msgs in #check printState

/-- info:
state: 7
final state is `7`
-/
#guard_msgs in #eval do
  let state := 7
  let (_, state) â† printState state
  println! "final state is `{state}`"



/-! It's tedious to have to "deconstruct" our monad transformer to run `M`-code.

Check out this class, and write the appropriate instance.
-/
#checkout MonadLift

section sol!
instance instMonadLift : MonadLift M (SMonT Ïƒ M) where
  monadLift a? state := do
    let a â† a?
    return (a, state)
end sol!

/-! This gives access to `liftM` which (here) can lift `IO`-code to `SMonT Ïƒ IO`-code. -/

def printState' [ToString Ïƒ] : SMonT Ïƒ IO Unit := do
  let state â† get
  liftM $ println! "state: {state}"

/-- info:
state: 7
final state is `7`
-/
#guard_msgs in #eval do
  let state := 7
  let (_, state) â† printState' state
  println! "final state is `{state}`"



/-! What if `M` is not really `IO`, but just something that `MonadLift`-s from `IO`? -/
def printState'' [MonadLift IO M] [ToString Ïƒ] : SMonT Ïƒ M Unit := do
  let state â† get
  liftM $ println! "state: {state}"

/-! This is too easy, now we can write functions like these. Right? -/

/-- error:
failed to synthesize instance
  MonadLift IO IO
-/
#guard_msgs in
def printState''IO [ToString Ïƒ] : SMonT Ïƒ IO Unit :=
  printState''

/-! We can't ğŸ™€

Let's actually check `liftM`'s type:
-/
#checkout liftM
/-! which leads us to: -/
#checkout MonadLiftT

/-! Let's try this again. -/
def printState''' [MonadLiftT IO M] [ToString Ïƒ] : SMonT Ïƒ M Unit := do
  let state â† get
  liftM $ println! "state: {state}"

def printState'''IO [ToString Ïƒ] : SMonT Ïƒ IO Unit :=
  printState'''

/-! Nice. -/

end SMonT
